--
--  Model for the AADL book.
--
--  More details on the book on
--  http://aadl-book.com/ and https://www.amazon.com/dp/B071WHRJY3
--
--
--  Copyright Â© 2016-2017 Julien Delange <julien@gunnm.org>
--  This work is free. You can redistribute it and/or modify it under the 
--  terms of the Do What The Fuck You Want To Public License, Version 2,
--  as published by Sam Hocevar. See the COPYING file for more details.
--


package aadlbook::component_definition
public

thread sender_thr
features
	valueout : out data port mydata;
properties
	Dispatch_Protocol => periodic;
	period => 20 ms;
end sender_thr;

process sender_pr 
features
	valueout : out data port mydata;
end sender_pr;

process implementation sender_pr.i
subcomponents
   thr : thread sender_thr;
connections
   c0 : port thr.valueout -> valueout;
end sender_pr.i;


thread processing_thr
features
	valuein  : in data port mydata;
	valueout : out data port mydata;
properties
	Dispatch_Protocol => periodic;
	period => 20 ms;
end processing_thr;

process processing_pr
features
	valuein  : in data port mydata;
	valueout : out data port mydata;
end processing_pr;

process implementation processing_pr.i
subcomponents
   thr : thread processing_thr;
connections
   c0 : port thr.valueout -> valueout;
   c1 : port valuein -> thr.valuein;
end processing_pr.i;


thread receiver_thr
features
	valuein : in data port mydata;
properties
	dispatch_protocol => periodic;
	period => 10 ms;
end receiver_thr;

process receiver_pr
features
	valuein : in data port mydata;
end receiver_pr;

process implementation receiver_pr.i
subcomponents
   thr : thread receiver_thr;
connections
	c0 : port valuein -> thr.valuein;
end receiver_pr.i;

bus ethernet
end ethernet;

data mydata
properties
	Data_Size => 1 bytes;
end mydata;

processor corei7
features
	eth : requires bus access ethernet;
end corei7;

system integration
end integration;

system implementation integration.i
subcomponents
   cpu0       : processor corei7;
   cpu1       : processor corei7;
   sender     : process sender_pr.i;
   processing : process processing_pr.i;
   receiver   : process receiver_pr.i;
   eth        : bus ethernet;
connections
   eth0     : bus access eth <-> cpu0.eth;
   eth1     : bus access eth <-> cpu1.eth;
   sender_to_processing   : port sender.valueout -> processing.valuein;
   processing_to_receiver : port processing.valueout -> receiver.valuein;
properties
	Actual_Connection_Binding => (reference (eth)) applies to sender_to_processing,processing_to_receiver;
end integration.i;

end aadlbook::component_definition;
